# vim: filetype=zsh
#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# OS X Specifics
if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
  export FZF_PREFIX="/usr/local/opt/fzf/shell"
fi

# Linux specifics
if [[ "${OSTYPE}" == linux* ]]; then
  export FZF_PREFIX="/usr/share/fzf"
fi

# Set editor's appropriately
if (( $+commands[nvim] )); then
  export EDITOR='nvim'
  export VISUAL='nvim'

  # Use neovim as man pager if we have it
  export MANPAGER="nvim -c 'set ft=man' -"
else
  export EDITOR='vim'
  export VISUAL='vim'
fi

export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# For golang
export GOPATH=$HOME/go
export PATH=$HOME/go/bin:$PATH

# For rustup
export PATH=$HOME/.cargo/bin:$PATH

# Always use user-defined programs before system-defined ones
export PATH=$HOME/bin:$PATH

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

#
# Temporary Files
#

# Use a non-random tmpdir
if [[ -z "$TMPDIR" ]]; then
  export TMPDIR="/tmp/zsh-$UID"
fi

# Create the tmpdir if it doesn't exist
if [[ ! -d "$TMPDIR" ]]; then
  mkdir "$TMPDIR"
  chmod 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"

#
# Secrets file
#
if [[ -f "$HOME/.secrets" ]]; then
  source "$HOME/.secrets"
fi
