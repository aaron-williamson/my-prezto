# vim: filetype=zsh
#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# OS X Specifics
if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'

  # For Visual Studio Code
  path+="/Applications/Visual Studio Code.app/Contents/Resources/app/bin"
fi

# Detect FZF Prefix if it exists
if [[ -d "$HOME/.fzf/shell" ]]; then
  export FZF_PREFIX="$HOME/.fzf/shell"
elif [[ -d "/usr/local/opt/fzf/shell" ]]; then
  export FZF_PREFIX="/usr/local/opt/fzf/shell"
elif [[ -d "/usr/share/fzf" ]]; then
  export FZF_PREFIX="/usr/share/fzf"
fi

# Set editor's appropriately
if (( $+commands[nvim] )); then
  export EDITOR='nvim'
  export VISUAL='nvim'

  # Use neovim as man pager if we have it
  export MANPAGER="nvim -c 'set ft=man' -"
else
  export EDITOR='vim'
  export VISUAL='vim'
fi

export PAGER='less'

# Set golang home
GOPATH=$HOME/go

# Set up some rust variables for use with vim
# Cache the result of rustc --print sysroot to speed up start time since
# zlogin is sourced every time a new shell is opened (improvement by ~50ms)
rust_sysroot_cache="$HOME/.rust_sysroot_cache"
if (( $+commands[rustc] )); then
  if [[ ! -f "$rust_sysroot_cache" ]]; then
    rustc --print sysroot > "$rust_sysroot_cache"
  fi

  export RUST_SRC_PATH=$(cat "$rust_sysroot_cache")/lib/rustlib/src/rust/src
fi

if (( $+commands[racer] )); then
  export RUST_RACER_PATH=$(which racer)
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  # Always use user-defined programs before system-defined ones
  $HOME/bin

  # Golang path
  $HOME/go/bin

  # For rustup
  $HOME/.cargo/bin

  # For FZF
  $HOME/.fzf/bin

  # For n - node version management
  /usr/local/n/bin

  /usr/local/{bin,sbin}

  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

#
# Secrets file
#
if [[ -f "$HOME/.secrets" ]]; then
  source "$HOME/.secrets"
fi
