# vim: filetype=zsh
#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# OS X Specifics
if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
  export FZF_PREFIX="/usr/local/opt/fzf/shell"
fi

# Linux specifics
if [[ "${OSTYPE}" == linux* ]]; then
  export FZF_PREFIX="/usr/share/fzf"
fi

# Set editor's appropriately
if (( $+commands[nvim] )); then
  export EDITOR='nvim'
  export VISUAL='nvim'

  # Use neovim as man pager if we have it
  export MANPAGER="nvim -c 'set ft=man' -"
else
  export EDITOR='vim'
  export VISUAL='vim'
fi

export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# For golang
GOPATH=$HOME/go
PATH=$HOME/go/bin:$PATH

# For rustup
PATH=$HOME/.cargo/bin:$PATH

# Always use user-defined programs before system-defined ones
PATH=$HOME/bin:$PATH

export PATH

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# Location of base16-shell for non true color terminals
export BASE16_SHELL="$HOME/.config/base16-shell/"

if [[ -d "$BASE16_SHELL" ]]; then
  export NO_TRUECOLOR="true"
fi

#
# Secrets file
#
if [[ -f "$HOME/.secrets" ]]; then
  source "$HOME/.secrets"
fi
